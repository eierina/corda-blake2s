buildscript {
  Properties constants = new Properties()
  file("$projectDir/constants.properties").withInputStream { constants.load(it) }

  ext {

    corda_release_group = constants.getProperty("cordaReleaseGroup")
    corda_core_release_group =  constants.getProperty("cordaCoreReleaseGroup")
    corda_release_version = constants.getProperty("cordaVersion")
    corda_core_release_version = constants.getProperty("cordaCoreVersion")
    corda_gradle_plugins_version = constants.getProperty("gradlePluginsVersion")

    kotlin_version = constants.getProperty("kotlinVersion")
    junit_version = constants.getProperty("junitVersion")
    quasar_version = constants.getProperty("quasarVersion")
    log4j_version = constants.getProperty("log4jVersion")
    slf4j_version = constants.getProperty("slf4jVersion")
    corda_platform_version = constants.getProperty("platformVersion").toInteger()
  }

  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-releases' }
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "net.corda.plugins:cordapp:$corda_gradle_plugins_version"
//    classpath "net.corda.plugins:cordformation:$corda_gradle_plugins_version"
//    classpath "net.corda.plugins:quasar-utils:$corda_gradle_plugins_version"
//    classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_gradle_plugin_version"
  }
}

allprojects {
  apply from: "${rootProject.projectDir}/repositories.gradle"
  apply plugin: 'java'
  apply plugin: 'kotlin'

  repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven { url 'https://software.r3.com/artifactory/corda' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-lib' }
    maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-lib-dev' }
  }

  tasks.withType(JavaCompile) {
    options.compilerArgs << "-parameters" // Required by Corda's serialisation framework.
  }

  jar {
    // This makes the JAR's SHA-256 hash repeatable.
    preserveFileTimestamps = false
    reproducibleFileOrder = true
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  }

  test {
    maxHeapSize = '2G'
    maxParallelForks = 4
    forkEvery = 4
  }
}

apply plugin: 'kotlin'
apply plugin: 'net.corda.plugins.cordapp'

defaultTasks 'build'

repositories {
  mavenLocal()
  jcenter()
  mavenCentral()
  maven { url 'https://software.r3.com/artifactory/corda' }
  maven { url 'https://jitpack.io' }
  maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-lib' }
  maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-lib-dev' }
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
  compileOnly "$corda_core_release_group:corda-core:$corda_core_release_version"
  testCompile "junit:junit:$junit_version"
}

jar {
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  baseName 'sha2sha3256'
}

wrapper {
  gradleVersion = '5.6.4'
  distributionType = Wrapper.DistributionType.ALL
}